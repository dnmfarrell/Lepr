#!/usr/bin/perl
use Scalar::Util 'looks_like_number';
use Storable qw(dclone);
use v5.16;
use warnings;

our $ENV = {
  atom  => sub { (1 == @_ && defined $_[0]) || undef },
  print => sub { say join ',', map { stringify($_) } @_ },
  split => sub { check_bin_args('split',@_);map \$_,split /${$_[0]}/,${$_[1]}},
  eq    => sub { check_bin_args('eq', @_) && (${$_[0]} eq ${$_[1]} || undef) },
  head  => sub { shift @_ },
  fold  => sub { my ($f,$id) = splice @_,0,2; $id = $f->($id,$_) for @_; $id },
  join  => sub { my $s = shift; \join $$s, map $$_, @_ },
  tail  => sub { @_[1..$#_] },
  map   => sub { my $f = shift; map { $f->($_) } @_ },
  '=='  => sub { check_num_args('==', @_) && ($_[0] == $_[1] || undef) },
  '>='  => sub { check_num_args('>=', @_) && ($_[0] >= $_[1] || undef) },
  '<='  => sub { check_num_args('<=', @_) && ($_[0] <= $_[1] || undef) },
  '>'   => sub { check_num_args('>',  @_) && ($_[0] >  $_[1] || undef) },
  '<'   => sub { check_num_args('<',  @_) && ($_[0] <  $_[1] || undef) },
  '+'   => sub { check_num_args('+',  @_) && $_[0] +  $_[1] },
  '-'   => sub { check_num_args('-',  @_) && $_[0] -  $_[1] },
  '/'   => sub { check_num_args('/',  @_) && $_[0] /  $_[1] },
  '*'   => sub { check_num_args('*',  @_) && $_[0] *  $_[1] },
  '^'   => sub { check_num_args('^',  @_) && $_[0] ** $_[1] },
  '%'   => sub { check_num_args('%',  @_) && $_[0] %  $_[1] },
};
run(join ' ', <DATA>, @ARGV);

sub check_bin_args {
  die sprintf "'%s' needs 2 args but got %d\n", $_[0], @_ - 1 unless @_ == 3}

sub check_num_args {
  check_bin_args(@_);
  die sprintf "'%s' operates on nums but got (%s)\n", $_[0],
    join ',', map { stringify($_) } @_[1..$#_]
    unless @_ - 1 == grep { looks_like_number($_) } @_[1..$#_] }

sub tokenize {
  my @chars = split //, shift;
  my ($t, @tokens) = ('');
  while (defined(my $c = shift @chars)) {
    if ($c eq '"') {
      push @tokens, $t if $t; $t = '';
      while (defined($c = shift @chars)) {
        if ($c eq '"') { my $str = $t; push @tokens, \$str; $t = '';last }
        else { $t .= $c }
      } die "Non-terminated string\n" if $t;
    }
    elsif ($c =~ /\s/)   { push @tokens, $t if length $t; $t = '' }
    elsif ($c =~ /[()]/) { push @tokens, $t if length $t; $t = ''; push @tokens, $c }
    else                 { $t .= $c }
  }
  \@tokens }

sub parse {
  my $t = shift @{$_[0]};
  if    (!defined $t || $t eq ')') { () }
  elsif ($t eq '(')                { [ parse(@_) ], parse(@_) }
  elsif ($t eq 'nil')              { undef, parse(@_) }
  else                             { $t, parse(@_) }}

sub evaluate {
  my ($expr, $env) = @_;
  return () unless @$expr;

  my $op = shift @$expr;
  if (!defined $op) {
    $op, evaluate($expr, $env);
  } elsif (ref $op eq 'SCALAR') {
    $op, evaluate($expr, $env);
  } elsif (ref $op eq 'ARRAY') {
    evaluate($op, $env), evaluate($expr, $env);
  } elsif ($op eq 'macro') {
    my $name = shift @$expr;
    $env->{"macro $name"} = fun($expr, $env);
    ();
  } elsif ($op eq 'fun') {
    if (ref $expr->[0]) { # anonymous fun
      fun($expr, $env);
    } else {
      my $fun_name = shift @$expr;
      $env->{$fun_name} = fun($expr, $env);
    }
  } elsif ($op eq 'if') {
    die sprintf "'if' needs 2 or 3 args but got %d\n", scalar @$expr
      unless @$expr >= 2 && @$expr <= 3;
    my ($cond, $then) = splice @$expr, 0, 2;
    ($cond) = evaluate([$cond], $env); # always eval in list context
    if (defined $cond) { evaluate([$then], $env) }
    elsif (@$expr) { evaluate([shift @$expr], $env) }
    else { () }
  } elsif ($op eq 'call') {
    my ($fun, @args) = evaluate($expr, $env);
    die "'call' needs a function as its first arg\n" unless ref $fun eq 'CODE';
    $fun->(@args);
  } elsif ($op eq 'set') {
    my $pairs = shift @$expr;
    die "'set' needs a list of pairs\n" if ref $pairs ne 'ARRAY' || !@$pairs;
    die "'set' needs an even number of keys & values\n" if @$pairs % 2;
    while (my $k = shift @$pairs) {
      die "Symbol '$k' is already set\n" if exists $env->{$k};
      $env->{$k} = shift @$pairs; # lazy
    }
    die "'set' needs a body\n" unless @$expr;
    evaluate($expr, $env);
  } elsif (looks_like_number($op)) {
    $op, evaluate($expr, $env);
  } elsif (my $macro = find("macro $op", $env)) {
      @$expr ? $macro->(@$expr) : $macro;
  } else {
    die "Symbol '$op' is undefined\n" unless exist($op, $env);
    my $symbol = find($op, $env);
    if (my $reftype = ref $symbol) {
      if ($reftype eq 'CODE') {
        @$expr ? $symbol->(evaluate($expr, $env)) : $symbol;
      } elsif ($reftype eq 'ARRAY') {
        evaluate(dclone($symbol), $env), evaluate($expr, $env);
      } else {
        $$symbol, evaluate($expr, $env);
      }
    } else {
      $symbol, evaluate($expr, $env);
    }
  }}

sub fun {
  my ($expr, $env) = @_;
  my $params = shift @$expr; # no evaluation of fun signatures
  die "'fun' needs a params list\n" unless $params && ref $params eq 'ARRAY' && @$params;
  die "'fun' needs a body\n" unless @$expr;
  sub {
    my (@args, %scope) = @_;
    for my $p (@$params) {
      if (substr($p, 0, 1) eq '@') { # a list param, empty list acceptable
        my $new_p = substr $p, 1;
        $scope{$new_p} = [splice @args];
      } else {
        die "Missing arg for function param '$p'\n" unless @args;
        $scope{$p} = shift @args;
      }
    }
    die sprintf "Excess args (%s) passed to function\n",
      join ',', map { stringify($_) } @args if @args;
    $scope{OUTER} = $env;
    evaluate(dclone($expr), \%scope);
  }}

sub stringify {
  my $s = shift;
  ref $s eq 'CODE' ? 'fun' : ref $s eq 'SCALAR' ? "\"$$s\"" : $s // 'nil' }

sub find {
  my ($k, $env) = @_;
  if (defined (my $v = $env->{$k})) { $v }
  elsif (my $outer = $env->{OUTER}) { find($k, $outer) }}

sub exist {
  my ($k, $env) = @_;
  if    ($k && exists $env->{$k})   { 1 }
  elsif (my $outer = $env->{OUTER}) { exist($k, $outer) }}

sub run {
  my ($code) = @_;
  my $tokens = tokenize($code);
  my @ast = parse([@$tokens]);
  evaluate(\@ast, $ENV)}
__DATA__
(fun id (x) x)
(fun filter (f @l)
  (if l (set (h (head l))
             (if (f h) h) filter (f) tail l)))
(fun sort (@nums)
  (if nums
      (set (h  (head nums)
            t  (tail nums)
            lt (filter (fun (n) <  n h) t)
            ge (filter (fun (n) >= n h) t))
           (sort lt) h sort ge)))
(fun take (x @l) (if (> x 0) ((head l) take (- x 1) tail l)))
(fun drop (x @l) (if (> x 0) (drop (- x 1) tail l) l))
(fun rev (@l) (if l ((rev tail l) head l)))
(macro && (x y) if x (if y y nil) nil)
(macro || (x y) if x x (if y y nil))
